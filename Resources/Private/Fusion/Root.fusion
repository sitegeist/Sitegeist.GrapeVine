include: resource://Neos.Fusion/Private/Fusion/Root.fusion

Sitegeist.GrapeVine.MessageController.index = Sitegeist.GrapeVine:MessageList
Sitegeist.GrapeVine.MessageController.show = Sitegeist.GrapeVine:MessageShow

prototype(Sitegeist.GrapeVine:MessageList) < prototype(Neos.Fusion:Component) {
    messages = ${messages}
    notifications = ${notifications}

    renderer = afx`
        <table class="neos-table">
            <thead>
                <tr>
                    <td>Title</td>
                    <td>Message</td>
                    <td>Recipient roles</td>
                    <td></td>
                </tr>
            </thead>
            <tbody>
                <Neos.Fusion:Loop items={props.messages} itemName="message">
                <tr>
                    <td>
                        <Neos.Fusion:Loop items={props.notifications} itemName="notification">
                            {notification.message == message ? '** NEW ** ' : ''}
                        </Neos.Fusion:Loop>
                        {message.title}
                    </td>
                    <td>{message.text}</td>
                    <td>{message.recipientRoleIdentifier}</td>
                    <td>
                        <Neos.Fusion:Link.Action
                            class="neos-button"
                            href.action="show"
                            href.arguments.message={message}
                        >
                            Show
                        </Neos.Fusion:Link.Action>
                        <Neos.Fusion:Loop items={props.notifications} itemName="notification">
                            <Neos.Fusion:Link.Action
                                @if={notification.message == message}
                                class="neos-button"
                                href.action="confirmNotification"
                                href.arguments.notification={notification}
                            >
                                Confirm
                            </Neos.Fusion:Link.Action>
                        </Neos.Fusion:Loop>
                    </td>
                </tr>
                </Neos.Fusion:Loop>
            </tbody>
        </table>
    `
}

prototype(Sitegeist.GrapeVine:MessageShow) < prototype(Neos.Fusion:Component) {
    message = ${message}

    renderer = afx`
        <h1>{props.message.title}</h1>
        <pre>{props.message.text}</pre>
        <pre>{props.message.recipeintRoleIdentifier}</pre>

        <Neos.Fusion:Link.Action
            class="neos-button"
            href.action="index"
        >
            Back
        </Neos.Fusion:Link.Action>

        <Neos.Fusion:Link.Action
            @if={notification}
            class="neos-button"
            href.action="confirmNotification"
            href.arguments.notification={notification}
        >
            Confirm Notification
        </Neos.Fusion:Link.Action>
    `
}
